# ###################################################################
#
# Mp3tag parsing for Libro.fm, created by romano on 2020-12-21
#
# Changes in the commits: https://github.com/romanoh/Mp3tag-Repository
#
# [28.11.2021] v.0.7
#	CHG: Results for search to 10
# 
# This file should be in your sources dir. 
# 
# ####################################################################

[Name]=Libro [v.0.7-28.11.2021]
[BasedOn]=www.libro.fm
[IndexUrl]=https://libro.fm/search?utf8=0x2713&q=
[AlbumUrl]=https://libro.fm
[WordSeperator]=+
[IndexFormat]=% ↓ Album%|% ↓ Author%|% ↓ Narrator%|% ↓ Lenght%|% ↓ Abridged%|%_url%
[SearchBy]=Enter Album||$regexp(%album%,'[- ]+cd ?\d+$',,1)||%s
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tagindex.txt"
#Debug "ON" "C:\Users\xxxx\Desktop\mp3tag_debug_libro_index.txt"	

#Only select the area we need instead of everyting.
findline "<div class=\"book-grid-item-info\">"
joinuntil "<!--close #search-content -->"

#remove some ads titles
replace "<div class="title">Give audiobook gift memberships and support local bookstores.</div>" "" 
replace "<div class="title">The perfect last-minute gift.</div>" ""

do	
	# Album
	findinline "<div class=\"title\">"
	sayuntil "<"
	say "|" 
	
	# Author
	findinline "class=\"author\">"
	sayuntil "<"
	say "|" 
	
	# Narrator
	findinline "narrators\">"
	sayuntil "<"
	say "|" 

	# Lenght
	findinline "Length:</strong> "
	if "<"
		say "Not yet released"
	else
		sayuntil "<br>"
	endif
	say "|"
	
	#  Abridged		
	findinline "Abridged:</strong> "
	sayuntil "</h6>"
	say "|"
	
	#  Url		
	findinline "no-margin\" href=\"" 
	sayuntil "\""
	#the last on insert the #	
	#say "|"
	
	saynewline
	 
   findinline "<div class=\"book-grid-item-info\">" 1 1
   
while " " 10

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tag.txt"
#Debug "ON" "C:\Users\xxx\Desktop\mp3tagdebugalbum.txt"

# Cover
outputto "Coverurl"
findline "og:image"
findinline "content=\""
sayuntil "\""

# Album
outputto "Album"
findline "<h1 itemprop=\"name\">"
findinline "name\">"
sayuntil "<"

# Series
outputto "SERIES"
findline "searchby=series" 1 1
findinline "\">" 1 1
sayuntil "<"
# Series-part
outputto "series-part"
replace " Book #" ""
findinline ":" 1 1
sayuntil "<"

# Subtitle
outputto "Subtitle"
findline "<h2>" 1 1
findinline "<h2>" 1 1
Sayuntil "</h2>"

# Author
gotoline 1
outputto "ArtistTMP1"
findline "By:</strong>"
findinline "authors\">"
sayuntil "</a>"
outputto "ArtistTMP2"
findinline "authors\">" 1 1
sayuntil "</a>"
outputto "ArtistTMP3"
findinline "authors\">" 1 1
sayuntil "</a>"
outputto "ArtistTMP4"
findinline "authors\">" 1 1
sayuntil "</a>"
outputto "ArtistTMP5"
findinline "authors\">" 1 1
sayuntil "</a>"

outputto "Artist"
sayoutput "ArtistTMP1"
IfOutput "ArtistTMP2"
	say ", "
	sayoutput "ArtistTMP2"
else
endif
IfOutput "ArtistTMP3"
	say ", "
	sayoutput "ArtistTMP3"
else
endif
IfOutput "ArtistTMP4"
	say ", "
	sayoutput "ArtistTMP4"
else
endif
IfOutput "ArtistTMP5"
	say ", "
	sayoutput "ArtistTMP5"
else
endif

set "ArtistTMP1"
set "ArtistTMP2"
set "ArtistTMP3"
set "ArtistTMP4"
set "ArtistTMP5"

# Composer
outputto "ComposerTMP1"
findline "Narrated by:</strong> "
findinline "Narrated by:</strong> "
findinline ">"
sayuntil "</a>"

outputto "ComposerTMP2"
findinline "\">" 1 1
sayuntil "</a>"

outputto "ComposerTMP3"
findinline "\">" 1 1
sayuntil "</a>"

outputto "ComposerTMP4"
findinline "\">" 1 1
sayuntil "</a>"

outputto "ComposerTMP5"
findinline "\">" 1 1
sayuntil "</a>"

outputto "ComposerTMP6"
findinline "\">" 1 1
sayuntil "</a>"

outputto "Composer"
sayoutput "ComposerTMP1"
IfOutput "ComposerTMP2"
	say ", "
	sayoutput "ComposerTMP2"
else
endif

IfOutput "ComposerTMP3"
	say ", "
	sayoutput "ComposerTMP3"
else
endif

IfOutput "ComposerTMP4"
	say ", "
	sayoutput "ComposerTMP4"
else
endif

IfOutput "ComposerTMP5"
	say ", "
	sayoutput "ComposerTMP5"
else
endif

IfOutput "ComposerTMP6"
	say ", "
	sayoutput "ComposerTMP6"
else
endif

set "ComposerTMP1"
set "ComposerTMP2"
set "ComposerTMP3"
set "ComposerTMP4"
set "ComposerTMP5"
set "ComposerTMP6"

# Lenght
outputto "Lenght"
findline "Length:</strong>"
replace " hours " ":"
replace " minutes" ""
replace " minute" ""
replace ":</span>" ":00"
findinline "Length:</strong> "
findinline ">"
sayuntil "<"

# Description
outputto "Comment"
findline "Description</h2>"
findline "<div itemprop=\"description\""
joinuntil "</div>"
# Use this to remove the html tags
regexpreplace "<[^>]+>" ""
sayrest

#ISBN
outputto "ISBN"
findline "ISBN"
joinuntil "</div>"
findinline "\">"
sayuntil "<"

#Language
outputto "Language"
findline "Language"
joinuntil "</div>"
findinline "\">"
sayuntil "<"

#Publisher
outputto "Publisher"
findline "Publisher"
joinuntil "</div>"
findinline "\">"
sayuntil "<"

#Year
outputto "year"
findline "Publication date"
joinuntil "</div>"
findinline "\">"
joinuntil "<"
findinline ", "
sayuntil "<"

#Edition
outputto "Edition"
findline "Edition"
joinuntil "</div>"
findinline "<span>"
sayuntil "<"

#Genres
outputto "GenreTMP1"
findline "Genres"
joinuntil "</div>"
findinline "\">" 1 1
if "<a href=\"/genres\">Genres</a></li>    </ul>"
say "No genre"
else
	sayuntil "<"
	
	outputto "GenreTMP2"
	findinline "\">" 1 1
	sayuntil "<"

	outputto "GenreTMP3"
	findinline "\">" 1 1
	sayuntil "<"

	outputto "GenreTMP4"
	findinline "\">" 1 1
	sayuntil "<"
endif

outputto "Genre"

IfOutput "GenreTMP1"
	sayoutput "GenreTMP1"
else
endif

IfOutput "GenreTMP2"
	say "/"
	sayoutput "GenreTMP2"
else
endif

IfOutput "GenreTMP3"
	say "/"
	sayoutput "GenreTMP3"
else
endif

IfOutput "GenreTMP4"
	say "/"
	sayoutput "GenreTMP4"
else
endif

set "GenreTMP1"
set "GenreTMP2"
set "GenreTMP3"
set "GenreTMP4"

# Set "Albumsort"
IfOutput "series"
	outputto "Albumsort"
	sayoutput "series"
	say " "
	sayoutput "series-part"
	say " "
	say "- "
	sayoutput "Year"
	say ". "
	sayoutput "album"
else
	outputto "Albumsort"
	sayoutput "Year"
	say ". "
	sayoutput "album"
endif

# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"

# Set Disknumber
outputto "DISCNUMBER"
sayoutput "series-part"

# Set Audible Album URL
outputto "WWWAUDIOFILE"
sayoutput "CurrentUrl"

# Set Comment to DESCRIPTION for better MP4 support
outputto "DESCRIPTION"
sayoutput "Comment"