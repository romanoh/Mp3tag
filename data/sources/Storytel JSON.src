[Name]=Storytel JSON [v.12.11.2022]
[BasedOn]=www.storytel.com
[IndexUrl]=https://www.storytel.com/api/search.action?q=%s
[AlbumUrl]=https://www.storytel.com/api/getBookInfoForContent.action?bookId=
[WordSeparator]=+
[IndexFormat]=%_url%|% ↓ Album%|% ↓ Author%|% ↓ Series%
[SearchBy]=Enter Album||$regexp(%album%,'[- ]+cd ?\d+$',,1)||%s
[UserAgent]=Storytel
[Encoding]=url-utf-8

# Used the Src Made by @Scuttle https://community.mp3tag.de/t/ws-storytel-json-based/58263
#
# Changed by Romano: changes in https://github.com/romanoh/Mp3tag
# 11.11.2022 - Added Isbn; fix no series error.
# 12.11.2022 - Changed to mofibo
# 12.11.2022 - Added Genre

[ParserScriptIndex]=...
#DebugWriteInput "C:\mp3debug\mp3tag.txt"
#Debug "ON" "C:\mp3debug\mp3tag_storytel_debug_index.txt"

json "ON" "current"

json_foreach "books"

	json_select "book"
	
		json_select "id"
			SayRest
			Say "|"
						
		json_select "name"
			SayRest
			Say "|"
			
		json_select "authorsAsString"
			SayRest
			SayNewLine
json_foreach_end

[ParserScriptAlbum]=...

#This should work, for some reason the 640x640 URLs to the covers are not in the API...
Replace "320x320" "640x640"

#DebugWriteInput "C:\mp3debug\mp3tag_album.txt"
#Debug "ON" "C:\Users\xxxx\Desktop\mp3tagdebug.txt"

json "ON" "current"

json_select_object "slb"

	json_select "abook"
	
		json_select "narratorAsString"
			OutputTo "COMPOSER"
			SayRest
			
		json_select "releaseDateFormat"
			OutputTo "YEAR"
			SayNChars 4
			
		json_select "releaseDateFormat"
			OutputTo "RELEASETIME"
			SayRest
			
		json_select "description"
			OutputTo "DESCRIPTION"
			SayRest
			
		json_select_object "publisher"
			json_select "name"
				OutputTo "PUBLISHER"
				SayRest		
		json_unselect_object
		
		json_select "isbn"
			OutputTo "ISBN"
			SayRest
			
json_unselect_object

json_select_object "slb"

	json_select "book"
		
		json_select "authorsAsString"
			OutputTo "ALBUMARTIST"
			SayRest
			
		json_select "authorsAsString"
			OutputTo "ARTIST"
			SayRest
			
		json_select_object "category"
			json_select "title"
			OutputTo "genre"
			SayRest
		json_unselect_object
		
		json_select "largeCover"
			OutputTo "COVERURL"
			Say "https://storytel.com"
			SayRest
			
		json_select "seriesOrder"
			ifnot "0"
				OutputTo "SERIES-PART"
				SayRest
				json_select_array "series" 1
					json_select "name"
						OutputTo "SERIES"
						SayRest
			endif
			
json_unselect_object
