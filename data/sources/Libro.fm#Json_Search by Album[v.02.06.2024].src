# ###################################################################
#
# Mp3tag parsing for Libro.fm, created by romano on 2020-12-21
#
# Current changes: https://github.com/romanoh/Mp3tag-Repository
#
# [28.11.2021]
#	CHG: Results for search to 10
# [01.07.2023]
#	NEW: starting working in new script as html changed.
# [23.09.2023]
#	NEW: complete rework of the album part using json
# [16.05.2024]
#	FIX: quick fix in index, so its works again.
#   NEW: cover thumbnails in index
# [02.06.2024]
#	FIX: quick fix in index, so its works again.
# 
# This file should be in your sources dir.
# ####################################################################

[Name]=Libro.fm Json version [v.02.06.2024]
[BasedOn]=www.libro.fm
[IndexUrl]=https://libro.fm/search?utf8=0x2713&q=
[AlbumUrl]=https://libro.fm
[WordSeperator]=+
[IndexFormat]=%_coverurl%|% ↓ Album%|% ↓ Author%|% ↓ Narrator%|% ↓ Lenght%|% ↓ Abridged%|%_url%
[SearchBy]=%artist% $regexp(%album%,'[- ]+cd ?\d+$',,1)
[Encoding]=url-utf-8
[MinAppVersionWin]=3.25c

[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#DebugWriteInput "C:\Users\xxx\Desktop\mp3tagindex.txt"
#Debug "ON" "C:\Users\xxx\Desktop\mp3tag_debug_libro_index.txt"	

#Only select the area we need instead of everyting.
findline "<div class=\"book-grid-item\">"
joinuntil "<footer id=\"footer\">"

#remove some ads titles
replace "<div class="title">Give audiobook gift memberships and support local bookstores.</div>" "" 
replace "<div class="title">The perfect last-minute gift.</div>" ""

do	
#  Cover
	
	findinline "class=\"book-cover\" src=\"/"
	say "https:/"	
	sayuntil "\""
	say "|" 
	
	# Album
	findinline "<div class=\"title\">"
	sayuntil "<"
	say "|" 
	
	# Author
	findinline "class=\"author\">"
	sayuntil "<"
	say "|" 
	
	# Narrator
	findinline "Narrated by:</strong>"
	sayuntil "<"
	say "|" 

	# Lenght
	findinline "Length:</strong> "
	if "<"
		say "Not yet released"
	else
		sayuntil "<"
	endif
	say "|"
	
	#  Abridged		
	findinline "Abridged:</strong> "
	sayuntil "<"
	say "|"
	
	#  Url		
	findinline "<a href=\"" 
	sayuntil "\""
	#the last on insert the #	
	#say "|"
		
	saynewline
	 
   findinline "<div class=\"book-grid-item-info\">" 1 1
   
while " " 3

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tag.txt"
#Debug "ON" "C:\Users\xxxx\Desktop\mp3tag_debug_album_libro.txt"

# Use the json inside the html
FindLine "<script type=\"application/ld+json\">"
JoinUntil "</script>"
# Use this to remove the html tags
RegexpReplace "<[^>]+>" ""
Replace "\\/" "/"
Unspace

json "on" "current"

# Album
OutputTo "ALBUM"
json_select "name"
SayRest

# Comments
OutputTo "comment"
json_select "description"
SayRest

# Isbn
OutputTo "isbn"
json_select "isbn"
SayRest	

# Cover
OutputTo "COVERURL"
json_select "image"
SayRest

# Cover
OutputTo "abridged"
json_select "abridged"
SayRest

# Artist 
outputto "artist"
json_foreach "author"
	ifnot "0"
		json_select_object "name"		
			json_select "name"
            ifoutput "artist"
				say "/"
			endif			
			sayrest
		json_unselect_object
	endif
json_foreach_end	
	
# narrator 
outputto "composer"
json_foreach "readBy"
	ifnot "0"
		json_select_object "name"		
			json_select "name"
            ifoutput "composer"
				say "/"
			endif			
			sayrest
		json_unselect_object
	endif
json_foreach_end	

# Publisher
OutputTo "publisher"
json_select "publisher"
SayRest

# Release_date
OutputTo "YEAR"
json_select "datePublished"
RegexpReplace "^.*?(?:^|\D)(\d{4})(?:\D|$).*$" "$1"
SayRegexp "^.*$"

# Language
OutputTo "language"
json_select "inLanguage"
SayRest

# Duration
OutputTo "duration"
json_select "duration"
replace "PT" ""
SayRest


json "off"

#----------------------------------------------------
#Genres
outputto "GENRE"
findline "genres" 1 1
Unspace
if "<div class=\"audiobook-genres\">"
joinuntil "</div>"

	outputto "GenreTMP1"
	findinline "href=\"" 1 1
	if "/"
		findinline ">"
		sayuntil "<"
	else
		say ""
	endif
	
	outputto "GenreTMP2"
	findinline "href=\"" 1 1
	if "/"
		findinline ">"
		sayuntil "<"
	else
		say ""
	endif

	outputto "GenreTMP3"
	findinline "href=\"" 1 1
	if "/"
		findinline ">"
		sayuntil "<"
	else
		say ""
	endif

	outputto "GenreTMP4"
	findinline "href=\"" 1 1
	if "/"
		findinline ">"
		sayuntil "<"
	else
		say ""
	endif
else

endif


outputto "Genre"

IfOutput "GenreTMP1"
	sayoutput "GenreTMP1"
else
endif

IfOutput "GenreTMP2"
	say "//"
	sayoutput "GenreTMP2"
else
endif

IfOutput "GenreTMP3"
	say "//"
	sayoutput "GenreTMP3"
else
endif

IfOutput "GenreTMP4"
	say "//"
	sayoutput "GenreTMP4"
else
endif

# remove tags
set "GenreTMP1"
set "GenreTMP2"
set "GenreTMP3"
set "GenreTMP4"

#----------------------------------------------------

# Series
gotoline 1
outputto "SERIES"
findline "audiobook-title__series" 1 1
findinline "title__series\">" 1 1
findinline ">" 1 1
sayuntil "</a>"

# Series-part
outputto "series-part"
replace " Book #" ""
findinline ":" 1 1
sayuntil "<"


# Set "Albumsort"
IfOutput "series"
	outputto "Albumsort"
	sayoutput "series"
	say " "
	sayoutput "series-part"
	say " "
	say "- "
	sayoutput "Year"
	say ". "
	sayoutput "album"
else
	outputto "Albumsort"
	sayoutput "Year"
	say ". "
	sayoutput "album"
endif

# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"

# Set Disknumber
outputto "DISCNUMBER"
sayoutput "series-part"

# Set Audible Album URL
outputto "WWWAUDIOFILE"
sayoutput "CurrentUrl"

# Set Comment to DESCRIPTION for better MP4 support
outputto "DESCRIPTION"
sayoutput "Comment"